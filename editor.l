(class +FromFile)

(dm load-from-file> (Name)
    (let Name (or Name (: name))
         (ifn (info Name)
              (out Name))
         (set (:: lines) (in Name (make (until (eof) (link (line T))))))))

(dm out-to-file> (Name)
    (let Name (or Name (: name))
         (out Name (prinl (text> This)))))

(class +Editor)

(dm handle-key> (Ch)
    (let (Frames (car *State) Focus (cadr *State) Mode (caddr *State))
      (setq Exit NIL)
      (cond
       ((= Mode 'normal) (cond
                          ((= Ch (char "q")) (setq Exit T))
                          ((= Ch (char "i"))
                           (setq Mode 'insert))
                          ((= Ch (char "A"))
                           (move-cursor> This
                                         (+ 1 (length (get-line> This (cursor-y> This))))
                                         (cursor-y> This))
                           (setq Mode 'insert))
                          ((= Ch (char "$"))
                           (move-cursor> This
                                         (+ 1 (length (get-line> This (cursor-y> This))))
                                         (cursor-y> This)))
                          ((= Ch (char "0"))
                           (move-cursor> This 1 (cursor-y> This)))
                          ((= Ch (char "o"))
                           (set (:: lines) (insert (+ 1 (: cursory)) (: lines) ""))
                           (move-cursor> This 1 (+ 1 (: cursory)))
                           (setq Mode 'insert))
                          ((= Ch (char "w")) (out-to-file> This))
                          ((= Ch (char "b")) (setq Focus (+ 1 (% Focus (length Frames)))))
                          ((= Ch (char "x"))
                           (adjust-cursor> This 1 0)
                           (delete-char> This))
                          ((= Ch (char "r"))
                           (adjust-cursor> This 1 0)
                           (delete-char> This)
                           (setq Mode 'insert))
                          ((= Ch (char "h")) (adjust-cursor> This -1 0))
                          ((= Ch (char "k")) (adjust-cursor> This 0 -1))
                          ((= Ch (char "j")) (adjust-cursor> This 0 1))
                          ((= Ch (char "l")) (adjust-cursor> This 1 0))))
       ((= Mode 'insert) (cond
                           ((= Ch *KEY-LEFT) (adjust-cursor> This -1 0))
                           ((= Ch *KEY-UP) (adjust-cursor> This 0 -1))
                           ((= Ch *KEY-DOWN) (adjust-cursor> This 0 1))
                           ((= Ch *KEY-RIGHT) (adjust-cursor> This 1 0))
                           ((= Ch *KEY-BACKSPACE) (delete-char> This))
                           ((= Ch 27)
                            (adjust-cursor> This -1 0)
                            (setq Mode 'normal))
                           (T (add-char> This (char Ch))))))
      (if Exit
          (let Res (list (delete This Frames) (max 1 (- (length Frames) 1)) Mode)
               (delete> This)
               (curses "clear" NIL)
               Res)
        (list Frames Focus Mode))))

(dm handle-mouse> (MX MY)
    (move-cursor> This MX MY)
    *State)

(dm handle-resize> ()
    (curses "clear" NIL)
    (set (:: width) (get-width Screen))
    (set (:: height) (get-height Screen))
    (destroy-window (: window))
    (set (:: window) (create-new-window (: x) (: y) (: width) (: height))))

(dm pos-inside> (MX MY)
    (and (>= MX (: x)) (>= MY (: y))
         (<= MY (+ (: height) (: y))) (<= MX (+ (: x) (: width)))))
